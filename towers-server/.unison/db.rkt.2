#lang racket/base
;;; Copyright (C) Laurent Orseau, 2010-2013
;;; GNU General Public Licence 3 (http://www.gnu.org/licenses/gpl.html)

(require (only-in towers-lib/connection encode-password)
         towers-lib/preferences
         bazaar/debug
         db/base
         db/mysql
         racket/list
         racket/string)

(provide get-connection
         set-connection
         set-auto-connection
         get-salt
         verify-user
         create-user
         get-players
         create-game
         get-game-list
         get-current-game-list
         get-game-elos
         get-game
         update-elo
         update-game
         get-players-stats)

;;; TODO: TESTS!!!
;;; create an ad-hoc db, and make tests on it

(define cnx #f)

(define (get-connection) cnx)

(define (close-connection)
  (when cnx
    (disconnect cnx)
    (set! cnx #f)))

(define (set-connection user password database)
  (close-connection)
  (set! 
   cnx
   ; See http://docs.racket-lang.org/db/using-db.html?q=db#%28part._intro-servlets%29
   (virtual-connection
    (connection-pool
     (λ()(mysql-connect #:user user
                        #:password password
                        #:database database))))))

;; Sets the connection from the preference file
(define (set-auto-connection)
  (set-connection (get-pref 'mysql-user) (get-pref 'mysql-password) (get-pref 'database)))

(define (get-pwd+salt user)
  (define pwd:salt
    (query-rows
     cnx
     "select password, salt from users where username=? and isPlayer=1 and block=0"
     user))
  (if (not (empty? pwd:salt))
      (apply values (vector->list (first pwd:salt)))
      (values #f #f)))

(define (get-salt user)
  (query-maybe-value 
   cnx
   "select salt from users where username=? and isPlayer=1 and block=0"
   user))

(define (verify-user user enc-pwd)
  (define-values (true-pwd salt) (get-pwd+salt user))
  (write (list enc-pwd true-pwd)) (newline)
  (and true-pwd
       (equal? true-pwd enc-pwd)))

; (verify-user "plip" (encode-password "plop" (get-salt "plip")))

(define (get-players)
  (query-list 
   cnx
   "select username from users where isPlayer=1 and block = 0"))

(define (create-game user user1 user2 size full-game next-player)
  (query ;-exec
   cnx
   "insert into games (username1, username2, size, fullGame, nextPlayer, lastUpdateDate, winner) 
    values (?, ?, ?, ?, ?, ?, '')"
   user1 user2 size full-game next-player (current-seconds))
  )

(define (get-game-list user [extra-cond ""])
  (query ;-rows
   cnx
   (string-append
    "select gameID, size, username1, username2, lastUpdateDate, nextPlayer, winner
    from games where (username1=? or username2=?) "
    extra-cond)
   user user))

(define (get-current-game-list user)
  (get-game-list user "and winner=''"))

(define (get-game game-id)
  (read
   (open-input-string
    (query-maybe-value 
     cnx
     "select fullGame from games where gameID=?"
     game-id))))

(define (get-game-elos game-id)
  (query-rows
   cnx
   "select username, elo from users, games 
    where gameID=? and (username=username1 or username=username2)"
   game-id))

(define (update-elo user elo)
  (query-exec cnx "update users set elo=? where username=?" elo user))

(define (update-game user game-id full-game next-player winner)
  (query-exec 
   cnx
   "update games set fullGame=?, nextPlayer=?, winner=?, lastUpdate=?
    where gameID=? and nextPlayer=?"
   full-game next-player winner (current-seconds) game-id user)
  )

(define (get-players-stats [start 0] [end 100])
  (query-rows
   cnx
   "select username, elo from users where isPlayer=1 and block=0 order by elo desc limit ?,?"
   start end))

(define (create-user user pwd salt email)
  ; TODO: verify that the salt is complex, to avoid attacks to guess the random generator
  ;(check-salt)
  ;(check-email)
  (query
   cnx
   "insert into users (name, username, email, password, salt, usertype, block, sendEmail, isPlayer, 
    gameNotifyEmail, elo)
    values (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)"
   user user email pwd salt "Registered" 0 1 1 1 1000))


;==========================;
;=== Database Structure ===;
;==========================;

;; Use this function to build a correct mysql name, surrounded by backticks,
;; and making sure it does not include backticks.
(define (sql-name name)
  (string-append "`" (string-replace name "`" "") "`"))

(define (select-database name)
  (query-exec cnx (string-append "USE " (sql-name name))))

;; exists: boolean
(define (drop-database name #:if-exists? [if-exists? #f])
  (query-exec
   cnx 
   (string-append "DROP DATABASE " (if if-exists? "IF EXISTS " "") (sql-name name))))

;; MySQL only?
;; More general: test 
(define (database-exists? name)
  (not
    (empty?
     (query-list cnx "SELECT SCHEMA_NAME FROM INFORMATION_SCHEMA.SCHEMATA WHERE SCHEMA_NAME = "
                 (sql-name name)))))

;; exists: (one-of 'error 'skip 'drop)
(define (create-database name 
                         #:exists [exists 'error])
  (when (eq? exists 'drop)
    (drop-database name #:if-exists? #t))
  (query-exec 
   cnx
   (string-append 
    "CREATE DATABASE "
    (if (memq exists '(skip drop))
        "IF NOT EXISTS "
        "")
    (sql-name name)
    " DEFAULT CHARACTER SET utf8 DEFAULT COLLATE utf8_general_ci")))

(define (create-towers-tables)
  (query-exec cnx "CREATE TABLE IF NOT EXISTS `games` (
  `gameID`          int(11) NOT NULL AUTO_INCREMENT,
  `username1`       varchar(100) NOT NULL,
  `username2`       varchar(100) NOT NULL,
  `size`            int(11) NOT NULL,
  `fullGame`        text NOT NULL,
  `nextPlayer`      varchar(100) NOT NULL,
  `lastUpdateDate`  int(11) NOT NULL,
  `finished`        tinyint(1) NOT NULL DEFAULT '0',
  `winner`          varchar(100) DEFAULT NULL,
  PRIMARY KEY (`gameID`)
) ENGINE=MyISAM  DEFAULT CHARSET=utf8 AUTO_INCREMENT=0")
  
  (query-exec cnx "CREATE TABLE IF NOT EXISTS `users` (
  `id`               int(11) NOT NULL AUTO_INCREMENT,
  `name`             varchar(255) NOT NULL DEFAULT '',
  `username`         varchar(150) NOT NULL DEFAULT '',
  `email`            varchar(100) NOT NULL DEFAULT '',
  `password`         varchar(100) NOT NULL DEFAULT '',
  `salt`             varchar(100) NOT NULL DEFAULT '',
  `usertype`         varchar(25) NOT NULL DEFAULT '',
  `block`            tinyint(4) NOT NULL DEFAULT '0',
  `sendEmail`        tinyint(4) DEFAULT '0',
  `isPlayer`         int(11) NOT NULL DEFAULT '1',
  `gameNotifyEmail`  int(11) NOT NULL DEFAULT '0',
  `elo`              int(11) NOT NULL DEFAULT '1000',
  PRIMARY KEY (`username`),
  KEY `usertype` (`usertype`),
  KEY `email` (`email`)
) ENGINE=MyISAM  DEFAULT CHARSET=utf8 AUTO_INCREMENT=1"))

(module+ test
  (require rackunit)
  (read-preferences)
  (set-auto-connection)
  
  (define db1 "test_db1_AUIEWWYI")
  
  (define-check (check-exn:fail expr)
    (check-exn exn:fail? (λ()expr)))
  
  (check-exn:fail (error "coucou"))
  (check-exn:fail "hello")
  
  ; Make sure the database exists, and select it
  (check-not-exn (λ()(drop-database db1 #:if-exists? #t)))
  (check-not-exn (λ()(create-database db1)))
  (check-not-exn (λ()(select-database db1)))
  
  (check-exn exn:fail? (λ()(create-database db1)))
  
  ; add a table to be sure it will be there after 'skip
  (check-not-exn (λ()(query-exec cnx "CREATE TABLE IF NOT EXISTS `table1` (
  `col1` int(11) NOT NULL
) ENGINE=InnoDB DEFAULT CHARSET=utf8;
")))
  (check-not-exn (λ()(create-database db1 #:exists 'skip)))
  ; table1 should still be here
  (check-not-exn (λ()(query cnx "SELECT * FROM table1")))
  
  ; then verify that the table gets dropped correctly:
  (check-not-exn (λ()(create-database db1 #:exists 'drop)))
  ; table1 should not be here anymore
  (check-exn exn:fail? (λ()(query cnx "SELECT * FROM table1")))
  )
