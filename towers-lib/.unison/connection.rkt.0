#lang racket/base
;;; Copyright (C) Laurent Orseau, 2010-2013
;;; GNU General Public Licence 3 (http://www.gnu.org/licenses/gpl.html)

#| Connection to server for games

This module provides functions to 
synchronise game files with a server.
(there must of course be a special server for that).

To authenticate a user, first the salt is requested to the server given a username.
The password is then encrypted (hashed and salted) on the client before being sent.
The password thus does NOT travel in plain text.


TODO :
- WRITE THE SPECIFICATIONS OF THE SERVER !
- the server should notify the players that they can play! (email?)

|#

(provide #|create-game-id get-game-id update-game-id |#
         encode-password
         game-server-address
         game-server-port
         game-root-path
         set-login-password
         check-authentication
         current-login
         current-server-version
         create-user
         get-game-list
         get-current-game-list
         update-game
         get-game
         new-game
         get-players
         )

(require bazaar/debug
         racket/list
         racket/port
         racket/match
         net/uri-codec
         net/url
         net/base64
         file/md5
         )

(define game-server-address     (make-parameter #f))
(define game-server-port        (make-parameter "80"))
(define game-root-path          (make-parameter "/"))
(define current-server-version  (make-parameter "0"))
(define current-login           (make-parameter #f))
(define current-password        (make-parameter ""))

;; Generates a new salt,
;; but it's not clear that this has the correct cryptographic properties.
;; DOES not generate strings of 32 chars?
(define (make-salt [n 32])
  (bytes->string/utf-8 
   (base64-encode 
    (list->bytes
     (build-list n (λ(n)(random 256))))
    "")))

(define (encode-password pwd salt)
  (bytes->string/utf-8 
   (md5 (string-append pwd salt))))

(define (set-current-password pwd [salt #f])
  (unless salt (set! salt (get-salt)))
  (current-password (encode-password pwd salt)))

(define (set-login-password l p [salt #f])
  (current-login l)
  (set-current-password p salt))

(define (basic-headers)
  (let* ([seconds (number->string (current-seconds))])
    (string-append
     "login=" (uri-encode (current-login))
     "&password=" (uri-encode (current-password))
     "&version=" (uri-encode (current-server-version))
     )))
   
(define (do-action action [other-headers '()])
  (define str
    (apply string-append
             "http://"
             (game-server-address) ;www.towers-game.net
             ":"
             (game-server-port)
             (game-root-path) ;/client-request/
             (current-server-version) ;1.6
             "?"
             (basic-headers)
             "&action=" action
             (map (λ(p)(string-append "&" (first p) "=" (uri-encode (second p))))
                  other-headers)))
  (displayln str)
  (define response 
    (port->list read (get-pure-port (string->url str))))
  (displayln "Received response:")
  (write response) (newline)
  (match response
    [(list `'(Error ,args ...))
     (apply error "Server error:" args)]
    [(list `',val) val]
    [(list val) val]
    [else (error "Unknown server error: unknown value:" response)]))

(define (create-user user pwd-txt email)
  (define salt (make-salt))
  (set-login-password user pwd-txt salt)
  (do-action "newuser" `(("salt" ,salt)("email" ,email))))

(define (get-game-list)
  (do-action "listgames"))

(define (get-current-game-list)
  (do-action "listcurrentgames"))

(define (get-game id)
  (do-action "getgame" `(("gameid" ,id))))
    
(define (new-game size opponent first-player? game)
  (do-action "newgame" 
             (list (list "login1" (if first-player? (current-login) opponent))
                   (list "login2" (if first-player? opponent (current-login)))
                   (list "game" game)
                   (list "size" size)
                   )))
    
; Should add finished, winner, etc. ?
(define (update-game game-id ply);game next-player winner)
  (do-action "updategame" 
             `(("gameid" ,game-id)
               ("ply" ,ply)
               ;("nextplayer" ,next-player)
               ;("game" ,game)
               ;("winner" ,winner)
               )))

(define (get-players)
  (do-action "getplayers"))

(define (get-salt)
  (do-action "getsalt"))

(define (check-authentication)
  (equal? (do-action "checkauth") '(#t)))

;;; See tests/connection.rkt for some tests
